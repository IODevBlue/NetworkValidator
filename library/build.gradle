plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'signing'
    id 'maven-publish'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 23
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant("release")
    }
}

def releaseVersion = '1.0.0-SNAPSHOT'

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'io.github.iodevblue'
                artifactId = 'networkvalidator'
                version = releaseVersion
                from components.release
                artifact androidJavadocJar
                artifact androidSourcesJar
                artifact androidJar
                pom {
                    name = 'Network Validator'
                    description = 'A lightweight module written in Kotlin for monitoring network state on native android.'
                    url = 'https://github.com/IODevBlue/NetworkValidator.git'
                    packaging = 'jar'

                    licenses {
                        license {
                            name = 'Apache License Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }

                    developers {
                        developer {
                            id = 'iodevblue'
                            name = 'IO DevBlue'
                            email = 'iodevblue@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/IODevBlue/NetworkValidator.git'
                        developerConnection = 'scm:git:ssh://github.com/IODevBlue/NetworkValidator.git'
                        url = 'https://github.com/IODevBlue/NetworkValidator'
                    }
                }
            }
        }
		
        repositories {
            def releaseUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            maven {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
                url = uri(releaseVersion.endsWith('SNAPSHOT')? snapshotUrl:releaseUrl)
            }
        }

    }

    signing {
        sign publishing.publications.maven
    }
}

dokkaHtml {
    moduleName.set("Network Validator")
    suppressInheritedMembers.set(true)
    offlineMode.set(true)
    dokkaSourceSets {
        configureEach {
            includes.from("module.md")
        }
    }
}

task androidSourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from android.sourceSets.main.kotlin.srcDirs
    doLast {
        copy {
            from androidSourcesJar
            into "$rootProject.projectDir/artefacts/$releaseVersion"
        }
    }
}

task androidJavadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    getArchiveClassifier().set('javadoc')
    from dokkaJavadoc
    doLast {
        copy {
            from androidJavadocJar
            into "$rootProject.projectDir/artefacts/$releaseVersion"
        }
    }
}

task androidJar(type: Jar) {
    from android.sourceSets.main.kotlin.sourceFiles
    doLast {
        copy {
            from androidJar
            into "$rootProject.projectDir/artefacts/$releaseVersion"
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}